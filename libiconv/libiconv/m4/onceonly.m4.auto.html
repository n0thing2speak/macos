<!DOCTYPE html PUBLIC "-//W3C//DTD XHTML 1.0 Strict//EN"
    "http://www.w3.org/TR/xhtml1/DTD/xhtml1-strict.dtd">
<html xmlns="http://www.w3.org/1999/xhtml" xml:lang="en" lang="en">
<head>
<title>onceonly.m4</title>
<style type="text/css">
.enscript-comment { font-style: italic; color: rgb(178,34,34); }
.enscript-function-name { font-weight: bold; color: rgb(0,0,255); }
.enscript-variable-name { font-weight: bold; color: rgb(184,134,11); }
.enscript-keyword { font-weight: bold; color: rgb(160,32,240); }
.enscript-reference { font-weight: bold; color: rgb(95,158,160); }
.enscript-string { font-weight: bold; color: rgb(188,143,143); }
.enscript-builtin { font-weight: bold; color: rgb(218,112,214); }
.enscript-type { font-weight: bold; color: rgb(34,139,34); }
.enscript-highlight { text-decoration: underline; color: 0; }
</style>
</head>
<body id="top">
<h1 style="margin:8px;" id="f1">onceonly.m4&nbsp;&nbsp;&nbsp;<span style="font-weight: normal; font-size: 0.5em;">[<a href="?txt">plain text</a>]</span></h1>
<hr/>
<div></div>
<pre>
<span class="enscript-comment"># onceonly.m4 serial 4 (gettext-0.15)
</span><span class="enscript-comment">dnl Copyright (C) 2002-2003, 2006 Free Software Foundation, Inc.
</span><span class="enscript-comment">dnl This file is free software; the Free Software Foundation
</span><span class="enscript-comment">dnl gives unlimited permission to copy and/or distribute it,
</span><span class="enscript-comment">dnl with or without modifications, as long as this notice is preserved.
</span>
<span class="enscript-comment">dnl This file defines some &quot;once only&quot; variants of standard autoconf macros.
</span><span class="enscript-comment">dnl   AC_CHECK_HEADERS_ONCE         like  AC_CHECK_HEADERS
</span><span class="enscript-comment">dnl   AC_CHECK_FUNCS_ONCE           like  AC_CHECK_FUNCS
</span><span class="enscript-comment">dnl   AC_CHECK_DECLS_ONCE           like  AC_CHECK_DECLS
</span><span class="enscript-comment">dnl   AC_REQUIRE([AC_HEADER_STDC])  like  AC_HEADER_STDC
</span><span class="enscript-comment">dnl The advantage is that the check for each of the headers/functions/decls
</span><span class="enscript-comment">dnl will be put only once into the 'configure' file. It keeps the size of
</span><span class="enscript-comment">dnl the 'configure' file down, and avoids redundant output when 'configure'
</span><span class="enscript-comment">dnl is run.
</span><span class="enscript-comment">dnl The drawback is that the checks cannot be conditionalized. If you write
</span><span class="enscript-comment">dnl   if some_condition; then gl_CHECK_HEADERS(stdlib.h); fi
</span><span class="enscript-comment">dnl inside an AC_DEFUNed function, the gl_CHECK_HEADERS macro call expands to
</span><span class="enscript-comment">dnl empty, and the check will be inserted before the body of the AC_DEFUNed
</span><span class="enscript-comment">dnl function.
</span>
<span class="enscript-comment">dnl This file is only needed in autoconf &lt;= 2.59.  Newer versions of autoconf
</span><span class="enscript-comment">dnl have this macro built-in.  But about AC_CHECK_DECLS_ONCE: note that in
</span><span class="enscript-comment">dnl autoconf &gt;= 2.60 the symbol separator is a comma, whereas here it is
</span><span class="enscript-comment">dnl whitespace.
</span>
<span class="enscript-comment">dnl Autoconf version 2.57 or newer is recommended.
</span>AC_PREREQ(2.54)

<span class="enscript-comment"># AC_CHECK_HEADERS_ONCE(HEADER1 HEADER2 ...) is a once-only variant of
</span><span class="enscript-comment"># AC_CHECK_HEADERS(HEADER1 HEADER2 ...).
</span>AC_DEFUN([AC_CHECK_HEADERS_ONCE], [
  :
  AC_FOREACH([gl_HEADER_NAME], [$1], [
    AC_DEFUN([gl_CHECK_HEADER_]m4_quote(<span class="enscript-keyword">translit</span>(m4_defn([gl_HEADER_NAME]),
                                                 [-./], [___])), [
      AC_CHECK_HEADERS(gl_HEADER_NAME)
    ])
    AC_REQUIRE([gl_CHECK_HEADER_]m4_quote(<span class="enscript-keyword">translit</span>(gl_HEADER_NAME,
                                                   [-./], [___])))
  ])
])

<span class="enscript-comment"># AC_CHECK_FUNCS_ONCE(FUNC1 FUNC2 ...) is a once-only variant of
</span><span class="enscript-comment"># AC_CHECK_FUNCS(FUNC1 FUNC2 ...).
</span>AC_DEFUN([AC_CHECK_FUNCS_ONCE], [
  :
  AC_FOREACH([gl_FUNC_NAME], [$1], [
    AC_DEFUN([gl_CHECK_FUNC_]m4_defn([gl_FUNC_NAME]), [
      AC_CHECK_FUNCS(m4_defn([gl_FUNC_NAME]))
    ])
    AC_REQUIRE([gl_CHECK_FUNC_]m4_defn([gl_FUNC_NAME]))
  ])
])

<span class="enscript-comment"># AC_CHECK_DECLS_ONCE(DECL1 DECL2 ...) is a once-only variant of
</span><span class="enscript-comment"># AC_CHECK_DECLS(DECL1, DECL2, ...).
</span>AC_DEFUN([AC_CHECK_DECLS_ONCE], [
  :
  AC_FOREACH([gl_DECL_NAME], [$1], [
    AC_DEFUN([gl_CHECK_DECL_]m4_defn([gl_DECL_NAME]), [
      AC_CHECK_DECLS(m4_defn([gl_DECL_NAME]))
    ])
    AC_REQUIRE([gl_CHECK_DECL_]m4_defn([gl_DECL_NAME]))
  ])
])
</pre>
<hr />
</body></html>