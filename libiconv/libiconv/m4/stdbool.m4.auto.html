<!DOCTYPE html PUBLIC "-//W3C//DTD XHTML 1.0 Strict//EN"
    "http://www.w3.org/TR/xhtml1/DTD/xhtml1-strict.dtd">
<html xmlns="http://www.w3.org/1999/xhtml" xml:lang="en" lang="en">
<head>
<title>stdbool.m4</title>
<style type="text/css">
.enscript-comment { font-style: italic; color: rgb(178,34,34); }
.enscript-function-name { font-weight: bold; color: rgb(0,0,255); }
.enscript-variable-name { font-weight: bold; color: rgb(184,134,11); }
.enscript-keyword { font-weight: bold; color: rgb(160,32,240); }
.enscript-reference { font-weight: bold; color: rgb(95,158,160); }
.enscript-string { font-weight: bold; color: rgb(188,143,143); }
.enscript-builtin { font-weight: bold; color: rgb(218,112,214); }
.enscript-type { font-weight: bold; color: rgb(34,139,34); }
.enscript-highlight { text-decoration: underline; color: 0; }
</style>
</head>
<body id="top">
<h1 style="margin:8px;" id="f1">stdbool.m4&nbsp;&nbsp;&nbsp;<span style="font-weight: normal; font-size: 0.5em;">[<a href="?txt">plain text</a>]</span></h1>
<hr/>
<div></div>
<pre>
<span class="enscript-comment"># Check for stdbool.h that conforms to C99.
</span>
<span class="enscript-comment">dnl Copyright (C) 2002-2004 Free Software Foundation, Inc.
</span><span class="enscript-comment">dnl This file is free software; the Free Software Foundation
</span><span class="enscript-comment">dnl gives unlimited permission to copy and/or distribute it,
</span><span class="enscript-comment">dnl with or without modifications, as long as this notice is preserved.
</span>
<span class="enscript-comment"># Prepare for substituting &lt;stdbool.h&gt; if it is not supported.
</span>
AC_DEFUN([AM_STDBOOL_H],
[
  AC_REQUIRE([AC_HEADER_STDBOOL])

  <span class="enscript-comment"># Define two additional variables used in the Makefile substitution.
</span>
  if test &quot;$ac_cv_header_stdbool_h&quot; = yes; then
    STDBOOL_H=''
  else
    STDBOOL_H='stdbool.h'
  fi
  AC_SUBST([STDBOOL_H])

  if test &quot;$ac_cv_type__Bool&quot; = yes; then
    HAVE__BOOL=1
  else
    HAVE__BOOL=0
  fi
  AC_SUBST([HAVE__BOOL])
])

<span class="enscript-comment"># This macro is only needed in autoconf &lt;= 2.59.  Newer versions of autoconf
</span><span class="enscript-comment"># have this macro built-in.
</span>
AC_DEFUN([AC_HEADER_STDBOOL],
  [AC_CACHE_CHECK([for stdbool.h that conforms to C99],
     [ac_cv_header_stdbool_h],
     [AC_TRY_COMPILE(
	[
	  <span class="enscript-comment">#include &lt;stdbool.h&gt;
</span>	  <span class="enscript-comment">#ifndef bool
</span>	   &quot;error: bool is not defined&quot;
	  <span class="enscript-comment">#endif
</span>	  <span class="enscript-comment">#ifndef false
</span>	   &quot;error: false is not defined&quot;
	  <span class="enscript-comment">#endif
</span>	  <span class="enscript-comment">#if false
</span>	   &quot;error: false is not 0&quot;
	  <span class="enscript-comment">#endif
</span>	  <span class="enscript-comment">#ifndef true
</span>	   &quot;error: true is not defined&quot;
	  <span class="enscript-comment">#endif
</span>	  <span class="enscript-comment">#if true != 1
</span>	   &quot;error: true is not 1&quot;
	  <span class="enscript-comment">#endif
</span>	  <span class="enscript-comment">#ifndef __bool_true_false_are_defined
</span>	   &quot;error: __bool_true_false_are_defined is not defined&quot;
	  <span class="enscript-comment">#endif
</span>
	  struct s { _Bool s: 1; _Bool t; } s;

	  char a[true == 1 ? 1 : -1];
	  char b[false == 0 ? 1 : -1];
	  char c[__bool_true_false_are_defined == 1 ? 1 : -1];
	  char d[(bool) -0.5 == true ? 1 : -1];
	  bool e = &amp;s;
	  char f[(_Bool) -0.0 == false ? 1 : -1];
	  char g[true];
	  char h[sizeof (_Bool)];
	  char i[sizeof s.t];
	  enum { j = false, k = true, l = false * true, m = true * 256 };
	  _Bool n[m];
	  char o[sizeof n == m * sizeof n[0] ? 1 : -1];
	],
	[
	  return (!a + !b + !c + !d + !e + !f + !g + !h + !i + !j + !k + !l
		  + !m + !n + !o);
	],
	[ac_cv_header_stdbool_h=yes],
	[ac_cv_header_stdbool_h=no])])
   AC_CHECK_TYPES([_Bool])
   if test $ac_cv_header_stdbool_h = yes; then
     AC_DEFINE(HAVE_STDBOOL_H, 1, [Define to 1 if stdbool.h conforms to C99.])
   fi])
</pre>
<hr />
</body></html>