<!DOCTYPE html PUBLIC "-//W3C//DTD XHTML 1.0 Strict//EN"
    "http://www.w3.org/TR/xhtml1/DTD/xhtml1-strict.dtd">
<html xmlns="http://www.w3.org/1999/xhtml" xml:lang="en" lang="en">
<head>
<title>eilseq.m4</title>
<style type="text/css">
.enscript-comment { font-style: italic; color: rgb(178,34,34); }
.enscript-function-name { font-weight: bold; color: rgb(0,0,255); }
.enscript-variable-name { font-weight: bold; color: rgb(184,134,11); }
.enscript-keyword { font-weight: bold; color: rgb(160,32,240); }
.enscript-reference { font-weight: bold; color: rgb(95,158,160); }
.enscript-string { font-weight: bold; color: rgb(188,143,143); }
.enscript-builtin { font-weight: bold; color: rgb(218,112,214); }
.enscript-type { font-weight: bold; color: rgb(34,139,34); }
.enscript-highlight { text-decoration: underline; color: 0; }
</style>
</head>
<body id="top">
<h1 style="margin:8px;" id="f1">eilseq.m4&nbsp;&nbsp;&nbsp;<span style="font-weight: normal; font-size: 0.5em;">[<a href="?txt">plain text</a>]</span></h1>
<hr/>
<div></div>
<pre>
<span class="enscript-comment">#serial 1
</span>
AC_PREREQ(2.50)

<span class="enscript-comment"># The EILSEQ errno value ought to be defined in &lt;errno.h&gt;, according to
</span><span class="enscript-comment"># ISO C 99 and POSIX.  But some systems (like SunOS 4) don't define it,
</span><span class="enscript-comment"># and some systems (like BSD/OS) define it in &lt;wchar.h&gt; not &lt;errno.h&gt;.
</span>
<span class="enscript-comment"># Define EILSEQ as a C macro and as a substituted macro in such a way that
</span><span class="enscript-comment"># 1. on all systems, after inclusion of &lt;errno.h&gt;, EILSEQ is usable,
</span><span class="enscript-comment"># 2. on systems where EILSEQ is defined elsewhere, we use the same numeric
</span><span class="enscript-comment">#    value.
</span>
AC_DEFUN([AC_EILSEQ],
[
  AC_REQUIRE([AC_PROG_CC])<span class="enscript-comment">dnl
</span>
  <span class="enscript-comment">dnl Check for any extra headers that could define EILSEQ.
</span>  AC_CHECK_HEADERS(wchar.h)

  AC_CACHE_CHECK([for EILSEQ], ac_cv_decl_EILSEQ, [
    AC_EGREP_CPP(yes,[
<span class="enscript-comment">#include &lt;errno.h&gt;
</span><span class="enscript-comment">#ifdef EILSEQ
</span>yes
<span class="enscript-comment">#endif
</span>      ], have_eilseq=1)
    if test -n &quot;$have_eilseq&quot;; then
      <span class="enscript-comment">dnl EILSEQ exists in &lt;errno.h&gt;. Don't need to define EILSEQ ourselves.
</span>      ac_cv_decl_EILSEQ=yes
    else
      AC_EGREP_CPP(yes,[
<span class="enscript-comment">#include &lt;errno.h&gt;
</span><span class="enscript-comment">#if HAVE_WCHAR_H
</span><span class="enscript-comment">#include &lt;wchar.h&gt;
</span><span class="enscript-comment">#endif
</span><span class="enscript-comment">#ifdef EILSEQ
</span>yes
<span class="enscript-comment">#endif
</span>        ], have_eilseq=1)
      if test -n &quot;$have_eilseq&quot;; then
        <span class="enscript-comment">dnl EILSEQ exists in some other system header.
</span>        <span class="enscript-comment">dnl Define it to the same value.
</span>        _AC_COMPUTE_INT([EILSEQ], ac_cv_decl_EILSEQ, [
<span class="enscript-comment">#include &lt;errno.h&gt;
</span><span class="enscript-comment">#if HAVE_WCHAR_H
</span><span class="enscript-comment">#include &lt;wchar.h&gt;
</span><span class="enscript-comment">#endif
</span>/* The following two lines are a workaround against an autoconf-2.52 bug.  */
<span class="enscript-comment">#include &lt;stdio.h&gt;
</span><span class="enscript-comment">#include &lt;stdlib.h&gt;
</span>])
      else
        <span class="enscript-comment">dnl EILSEQ isn't defined by the system. Define EILSEQ ourselves, but
</span>        <span class="enscript-comment">dnl don't define it as EINVAL, because iconv() callers want to
</span>        <span class="enscript-comment">dnl distinguish EINVAL and EILSEQ.
</span>        ac_cv_decl_EILSEQ=ENOENT
      fi
    fi
  ])
  if test &quot;$ac_cv_decl_EILSEQ&quot; != yes; then
    AC_DEFINE_UNQUOTED([EILSEQ], [$ac_cv_decl_EILSEQ],
                       [Define as good substitute value for EILSEQ.])
    EILSEQ=&quot;$ac_cv_decl_EILSEQ&quot;
    AC_SUBST(EILSEQ)
  fi
])
</pre>
<hr />
</body></html>