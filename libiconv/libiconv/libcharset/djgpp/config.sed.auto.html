<!DOCTYPE html PUBLIC "-//W3C//DTD XHTML 1.0 Strict//EN"
    "http://www.w3.org/TR/xhtml1/DTD/xhtml1-strict.dtd">
<html xmlns="http://www.w3.org/1999/xhtml" xml:lang="en" lang="en">
<head>
<title>config.sed</title>
<style type="text/css">
.enscript-comment { font-style: italic; color: rgb(178,34,34); }
.enscript-function-name { font-weight: bold; color: rgb(0,0,255); }
.enscript-variable-name { font-weight: bold; color: rgb(184,134,11); }
.enscript-keyword { font-weight: bold; color: rgb(160,32,240); }
.enscript-reference { font-weight: bold; color: rgb(95,158,160); }
.enscript-string { font-weight: bold; color: rgb(188,143,143); }
.enscript-builtin { font-weight: bold; color: rgb(218,112,214); }
.enscript-type { font-weight: bold; color: rgb(34,139,34); }
.enscript-highlight { text-decoration: underline; color: 0; }
</style>
</head>
<body id="top">
<h1 style="margin:8px;" id="f1">config.sed&nbsp;&nbsp;&nbsp;<span style="font-weight: normal; font-size: 0.5em;">[<a href="?txt">plain text</a>]</span></h1>
<hr/>
<div></div>
<pre>
# Additional editing of Makefiles
/(echo[ 	]*':t/ a\
# DJGPP specific Makefile changes.\
  /^aliaspath *	*=/s,:,&quot;;&quot;,g;t t\
  /TEXINPUTS=/s,:,&quot;;&quot;,g;t t\
  /PATH=/s,:,&quot;;&quot;,g;t t\
  s,\\.deps,_deps,g;t t\
  s,\\.libs,_libs,g;t t\
  s,\\.new\\.,_new.,g;t t\
  s,\\.old\\.,_old.,g;t t\
  s,\\.tab\\.,_tab.,g;t t\
  /^docdir *	*=/s,/doc/,/gnudocs/,;t t\
  s,config\\.h\\.in,config.h-in,g;t t\
  s,\\.\\([1-9]\\)\\.html,.\\1-html,g;t t\
  s,\\.html,-html,g;t t\
  s,/doc,/gnudocs,g;t t\
  s,libcharset\\.h\\.in,libcharset.h-in,g;t t\
  s,localcharset\\.h\\.in,localcharset.h-in,g;t t\
  s,Makefile\\.in\\.in,Makefile.in-in,g;t t\
  s,Makefile\\.am\\.in,Makefile.am-in,g;t t\
  /^lispdir *	*=/ c\\\
lispdir = \$(prefix)/gnu/emacs/site-lisp\
  /^install-info-am:/,/^$/ s,file-\\[0-9\\]\\[0-9\\],&amp; \\$\\$file[0-9] \\$\\$file[0-9][0-9],\
  /^\\.y\\.c:/,/^$/ {\
    /\\\$(YACC)/ {\
      s,[ 	]*&amp;&amp;.*$,,\
      a\\\
	-mv -f y_tab.c \$*.c\\\
	-@test -f y.tab.c &amp;&amp; mv -f y.tab.c \$*.c\\\
	-@test -f y.tab.h &amp;&amp; mv -f y.tab.h y_tab.h\
    }\
  }

# Rename config.h.in into config.h-in
/^[ 	]*ac_config_headers=/,/^_ACEOF/ {
  s|config\.h|&amp;:config.h-in|g
}
/CONFIG_HEADERS=/ s|config\.h\.in|&amp;:config.h-in|2

# We always use _deps and _libs instead of .deps and .libs, because
# the latter is an invalid name on 8+3 MS-DOS filesystem.  This makes
# the generated Makefiles good for every DJGPP installation, not only
# the one where the package was configured (which could happen to be
# a Windows box, where leading dots in file names are allowed).
s,\.deps,_deps,g
s,\.libs,_libs,g
/^rmdir[ 	]*\.tst/ i\
am__leading_dot=_

# Replace (command) &gt; /dev/null with `command &gt; /dev/null`, since
# parenthesized commands always return zero status in the ported Bash,
# even if the named command doesn't exist
/if ([^|;`]*null/{
  s,(,`,
  s,),,
  /null[ 	]*2&gt;&amp;1/ s,2&gt;&amp;1,&amp;`,
  /null.*null/ s,null.*null,&amp;`,
  /null.*null/ !{
    /null[ 	]*2&gt;&amp;1/ !s,null,&amp;`,
  }
}

# DOS-style absolute file names should be supported as well
/\*) srcdir=/s,/\*,[\\\\/]* | [A-z]:[\\\\/]*,
/\$]\*) INSTALL=/s,\[/\$\]\*,[\\\\/$]* | [A-z]:[\\\\/]*,
/\$]\*) ac_rel_source=/s,\[/\$\]\*,[\\\\/$]* | [A-z]:[\\\\/]*,

# Switch the order of the two Sed commands, since DOS path names
# could include a colon
/ac_file_inputs=/s,\( -e &quot;s%\^%\$ac_given_srcdir/%&quot;\)\( -e &quot;s%:% $ac_given_srcdir/%g&quot;\),\2\1,

# Prevent the spliting of conftest.subs.
# The sed script: conftest.subs is split into 48 or 90 lines long files.
# This will produce sed scripts called conftest.s1, conftest.s2, etc.
# that will not work if conftest.subs contains a multi line sed command
# at line #90. In this case the first part of the sed command will be the
# last line of conftest.s1 and the rest of the command will be the first lines
# of conftest.s2. So both script will not work properly.
# This matches the configure script produced by Autoconf 2.57
/ac_max_sed_lines=[0-9]/ s,=.*$,=`sed -n &quot;$=&quot; $tmp/subs.sed`,

# The following two items are changes needed for configuring
# and compiling across partitions.
# 1) The given srcdir value is always translated from the
#    &quot;x:&quot; syntax into &quot;/dev/x&quot; syntax while we run configure.
/^[ 	]*-srcdir=\*.*$/ a\
    ac_optarg=`echo &quot;$ac_optarg&quot; | sed &quot;s,^\\([A-Za-z]\\):,/dev/\\1,&quot;`
/set X `ls -Lt \$srcdir/ i\
   if `echo $srcdir | grep &quot;^/dev/&quot; - &gt; /dev/null`; then\
     srcdir=`echo &quot;$srcdir&quot; | sed -e &quot;s%^/dev/%%&quot; -e &quot;s%/%:/%&quot;`\
   fi

#  2) We need links across partitions,
#     so we will use &quot;cp -pf&quot; instead of &quot;ln&quot;.
/# Make a symlink if possible; otherwise try a hard link./,/EOF/ {
  s,;.*then, 2&gt;/dev/null || cp -pf \$srcdir/\$ac_source \$ac_dest&amp;,
}

# Autoconf 2.52e generated configure scripts
# write absolute paths into Makefiles making
# them useless for DJGPP installations for which
# the package has not been configured for.
/MISSING=/,/^$/ {
  /^fi$/ a\
am_missing_run=`echo &quot;$am_missing_run&quot; | sed 's%/dev.*/libicharset-[0-9]\\{1,1\\}[-.0-9]*%${top_srcdir}%'`
}
/^install_sh=/a\
install_sh=`echo &quot;$install_sh&quot; | sed 's%/dev.*/libicharset-[0-9]\\{1,1\\}[-.0-9]*%${top_srcdir}%'`

# The following makes sure we are not going to remove a directory
# which is the cwd on its drive (DOS doesn't allow to remove such
# a directory).  The trick is to chdir to the root directory on
# temp directory's drive before removing $tmp.
/^[ 	]*trap[ 	]*'exit_status=\$\?;[ 	]*rm[ 	]*-rf/ s%rm -rf%cd $tmp; cd /; &amp;%

# AC_CONFIG_LINKS fails if the source and destination are on
# different file systems and symlinks don't work.
/^    ln \$srcdir/s%||%|| cp -pf $srcdir/$ac_source $ac_dest ||%

# Add DJGPP version information.
/^[ 	]*VERSION=/ s/\([0-9]\.[0-9]\.[0-9]\)/\1  (DJGPP port 2006-03-21 (r1))/

# We need makeinfo to make the html formated docs.
/\$am_missing_run[ 	]*makeinfo/ s,\$am_missing_run,,

# The path to the FORTRAN compiler and libraries
# shall contain no absolute path reference so it
# will be good for all djgpp installations.
/^FLIBS=&quot;\$ac_cv_flibs&quot;/ i\
ac_djgpp_path=`echo &quot;$DJDIR&quot; | sed 's%\\\\\\%/%g' | tr $as_cr_LETTERS $as_cr_letters`\
ac_cv_flibs=`echo &quot;$ac_cv_flibs&quot; | sed &quot;s%-L$ac_djgpp_path%-L/dev/env/DJDIR%g&quot;`

# Let libtool use _libs all the time.
/objdir=/s,\.libs,_libs,
</pre>
<hr />
</body></html>